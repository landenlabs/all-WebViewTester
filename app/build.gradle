// All-WebViewTester
// module build.gradle
apply plugin: 'com.android.application'

android {
    namespace "com.landenlabs.all_webviewtester"
    compileSdk 34

    defaultConfig {
        applicationId 'com.landenlabs.all_webviewtester'
        minSdkVersion 23
        targetSdkVersion 34

        versionCode 100128
        versionName '13.01.05'
    }

    // Save app version in string so resource can be used in layouts
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", "version " + variant.versionName
    }

    buildFeatures.buildConfig = true

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute',
                'FieldCanBeLocal', 'LocalSuppress', 'HardcodedText', 'Typos'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Enable verbose lint reporting during build.
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    /*
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
     */
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
}

// Rename app
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def ourName = project.parent.name
        outputFileName = "$ourName-${variant.name}-${variant.versionName}.apk"
        println "out name= $outputFileName"
    }
}